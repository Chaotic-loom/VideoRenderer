plugins {
    id 'multiloader-loader'
    id 'fabric-loom'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}
dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${parchment_minecraft}:${parchment_version}@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

    // FFMPEG Java Interfaces
    implementation "org.bytedeco:javacv:$javacvVersion"
    implementation "org.bytedeco:javacpp:$javacvVersion"
    implementation "org.bytedeco:ffmpeg:$ffmpegVersion"

    // Linux x64
    implementation "org.bytedeco:javacpp:$javacvVersion:linux-x86_64"
    implementation "org.bytedeco:ffmpeg:$ffmpegVersion:linux-x86_64"

    // Windows x64
    implementation "org.bytedeco:javacpp:$javacvVersion:windows-x86_64"
    implementation "org.bytedeco:ffmpeg:$ffmpegVersion:windows-x86_64"

    // MacOS
    implementation "org.bytedeco:javacpp:$javacvVersion:macosx-x86_64"
    implementation "org.bytedeco:ffmpeg:$ffmpegVersion:macosx-x86_64"
}

loom {
    def aw = project(':common').file("src/main/resources/${mod_id}.accesswidener")
    if (aw.exists()) {
        accessWidenerPath.set(aw)
    }
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName('Fabric Client')
            ideConfigGenerated(true)
            runDir('runs/client')
        }
        server {
            server()
            setConfigName('Fabric Server')
            ideConfigGenerated(true)
            runDir('runs/server')
        }
    }
}

shadowJar {
    configurations = [project.configurations.runtimeClasspath] // Include natives
    mergeServiceFiles()
    relocate 'org.bytedeco', 'com.chaotic_loom.video_renderer.shadow.org.bytedeco'
    archiveClassifier.set('dev-shadow')
}

tasks.named('remapJar') {
    inputFile.set(shadowJar.archiveFile)
    dependsOn shadowJar
}